SECTIONS {
	.text : {
		__exidx_start = .;
		
		/* Vector table. */
		VectorsStart = .;
		
		LONG(StackEnd)
		KEEP(*(.Reset))
		KEEP(*(.NMI))
		KEEP(*(.HardFault))
		KEEP(*(.MemManage))
		KEEP(*(.BusFault))
		KEEP(*(.UsageFault))
		KEEP(*(.Reserved7))
		KEEP(*(.Reserved8))
		KEEP(*(.Reserved9))
		KEEP(*(.Reserved10))
		KEEP(*(.SVCall))		
		KEEP(*(.DebugMon))		
		KEEP(*(.Reserved13))		
		KEEP(*(.PendSV))		
		KEEP(*(.SysTick))
				
		ASSERT(. - VectorsStart==16*4,"Bad number of system exception vectors");
		
		KEEP(*(.ISRs))
		
		ASSERT(. - VectorsStart <= 256*4, "To many external interrupt vectors");			
		/* Special boot code. */
		. = DEFINED(bootOffset) ? bootOffset : .;
		*(.text.bootcode)

		/* Code. */ 
		*(.text .text.*)       
	} >loadTo
	
	.rodata : {
		. = ALIGN(4);
		*(
			SORT_BY_ALIGNMENT(.rodata)
			SORT_BY_ALIGNMENT(.rodata.*)
		)
		
		. = ALIGN(4);
		__exidx_end = .;
	} >loadTo
	
	.stack : {} >RAM
	
	StackBegin = ALIGN(ADDR(.stack), (1 << StackLog2) / 8);
	StackEnd = StackBegin + (1 << StackLog2) * StackFrac / 8 * (MaxTasks + 1);
       
	.data StackEnd : {
		. = ALIGN(4);
		*(
			SORT_BY_ALIGNMENT(.data)
			SORT_BY_ALIGNMENT(.data.*)
		)
		. = ALIGN(4);
	} >RAM AT>loadTo
	
	DataRAM = ADDR(.data);
	DataLoad = LOADADDR(.data);
	DataSize = SIZEOF(.data);
	
	.bss : {
		. = ALIGN(4);
		*(
			SORT_BY_ALIGNMENT(.bss)
			SORT_BY_ALIGNMENT(.bss.*)
		)
		*(SORT_BY_ALIGNMENT(COMMON))
		. = ALIGN(4);
	} >RAM
	
	BSSRAM = ADDR(.bss);
	BSSSize = SIZEOF(.bss);

	HeapBegin = BSSRAM + BSSSize;
	HeapEnd = ORIGIN(RAM) + LENGTH(RAM);
	
	.dummy 0 (DSECT) : {
		*(.dummy .dummy.*)
	}
}

ENTRY(runtime$noos$reset)