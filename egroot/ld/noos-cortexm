SECTIONS {
	.text : {
		__exidx_start = .;
		
		/* Vector table. */
		LONG(StackEnd)
		KEEP(*(.Reset))
		KEEP(*(.NMI))
		KEEP(*(.HardFault))
		KEEP(*(.MemManage))
		KEEP(*(.BusFault))
		KEEP(*(.UsageFault))
		KEEP(*(.Reserved7))
		KEEP(*(.Reserved8))
		KEEP(*(.Reserved9))
		KEEP(*(.Reserved10))
		KEEP(*(.SVCall))		
		KEEP(*(.DebugMon))		
		KEEP(*(.Reserved13))		
		KEEP(*(.PendSV))		
		KEEP(*(.SysTick))		
		KEEP(*(.ISRs))
		
		/* Code */ 
		*(.text .text.*)       
	} >loadTo
	
	.rodata : {
		. = ALIGN(4);
		*(
			SORT_BY_ALIGNMENT(.rodata)
			SORT_BY_ALIGNMENT(.rodata.*)
		)
		
		. = ALIGN(4);
		__exidx_end = .;
	} >loadTo
    
    .stack : {
    	. = ALIGN((1 << StackLog2) / 8);
    	. = . + (1 << StackLog2) * StackFrac / 8 * (MaxTasks + 1);
    } >RAM
    
    StackEnd = ADDR(.stack) + SIZEOF(.stack);
    
	.data : {
		. = ALIGN(4);
		*(
			SORT_BY_ALIGNMENT(.data)
			SORT_BY_ALIGNMENT(.data.*)
		)
		. = ALIGN(4);
	} >RAM AT>loadTo
	
	DataRAM = ADDR(.data);
	DataLoad = LOADADDR(.data);
	DataSize = SIZEOF(.data);
	
	.bss : {
		. = ALIGN(4);
		*(
			SORT_BY_ALIGNMENT(.bss)
			SORT_BY_ALIGNMENT(.bss.*)
		)
		. = ALIGN(4);
	} >RAM
	
	BSSRAM = ADDR(.bss);
	BSSSize = SIZEOF(.bss);

	HeapBegin = BSSRAM + BSSSize;
	HeapEnd = ORIGIN(RAM) + LENGTH(RAM);
	
	.noload 0 (NOLOAD) : {
		*(.noload .noload.*)
	}
}
