SECTIONS {
	.text : {
		__exidx_start = .;
		KEEP(*(.vectors))    /* Interrupt vector table */
		*(.text .text.*)     /* Code */   
	} >loadTo
	
	
	.rodata : {
		. = ALIGN(4);
		*(.rodata .rodata.*) /* Read only data */
		. = ALIGN(4);
		__exidx_end = .;
	} >loadTo
    
    .stack : {
    	. = ALIGN((1 << StackExp) / 8);
    	. = . + (1 << StackExp) * StackFrac / 8 * (MaxTasks + 1);
    } >RAM
    
    StackEnd = ADDR(.stack) + SIZEOF(.stack);
    
	.data : {
		. = ALIGN(4);
		*(.data .data.*)
		. = ALIGN(4);
	} >RAM AT>loadTo
	
	DataRAM = ADDR(.data);
	DataLoad = LOADADDR(.data);
	DataSize = SIZEOF(.data);
	
	.bss : {
		. = ALIGN(4);
		*(.bss .bss.*)
		. = ALIGN(4);
	} >RAM
	
	BSSRAM = ADDR(.bss);
	BSSSize = SIZEOF(.bss);
}

HeapBegin = BSSRAM + BSSSize;
HeapSize = ORIGIN(RAM) + LENGTH(RAM) - HeapBegin;

ENTRY(runtime$noos$Start)
