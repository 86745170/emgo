SECTIONS {
	.text : {
		__exidx_start = .;
		KEEP(*(.vectors))    /* Interrupt vector table */
		*(.text .text.*)     /* Code */   
	} >loadTo
	
	
	.rodata : {
		. = ALIGN(4);
		*(
			SORT_BY_ALIGNMENT(.rodata)
			SORT_BY_ALIGNMENT(.rodata.*)
		)
		. = ALIGN(4);
		__exidx_end = .;
	} >loadTo
    
    .stack : {
    	. = ALIGN((1 << StackExp) / 8);
    	. = . + (1 << StackExp) * StackFrac / 8 * (MaxTasks + 1);
    } >RAM
    
    StackEnd = ADDR(.stack) + SIZEOF(.stack);
    
	.data : {
		. = ALIGN(4);
		*(
			SORT_BY_ALIGNMENT(.data)
			SORT_BY_ALIGNMENT(.data.*)
		)
		. = ALIGN(4);
	} >RAM AT>loadTo
	
	DataRAM = ADDR(.data);
	DataLoad = LOADADDR(.data);
	DataSize = SIZEOF(.data);
	
	.bss : {
		. = ALIGN(4);
		*(
			SORT_BY_ALIGNMENT(.bss)
			SORT_BY_ALIGNMENT(.bss.*)
		)
		. = ALIGN(4);
	} >RAM
	
	BSSRAM = ADDR(.bss);
	BSSSize = SIZEOF(.bss);

	HeapBegin = BSSRAM + BSSSize;
	HeapEnd = ORIGIN(RAM) + LENGTH(RAM);
	
	.unused HeapEnd : {
		. = ALIGN(4);
		*(
			SORT_BY_ALIGNMENT(.unused)
			SORT_BY_ALIGNMENT(.unused.*)
		)
		. = ALIGN(4);
	}
}

ENTRY(runtime$noos$Start)
