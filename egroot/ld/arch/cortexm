SECTIONS {
	.text : {
		KEEP(*(.vectors))    /* Interrupt vector table */
		*(.text .text.*)     /* Code */   
	} >loadTo
	
	.rodata : {
		. = ALIGN(4);
		*(.rodata .rodata.*) /* Read only data */
		. = ALIGN(4);
	} >loadTo
    
	.data : {
		. = ALIGN(4);
		*(.data .data.*)
		. = ALIGN(4);
	} >RAM AT>loadTo
	
	DataRAM = ADDR(.data);
	DataLoad = LOADADDR(.data);
	DataSize = SIZEOF(.data);
	
	.bss : {
		. = ALIGN(4);
		*(.bss .bss.*)
		. = ALIGN(4);
	} >RAM
	
	BSSRAM = ADDR(.bss);
	BSSSize = SIZEOF(.bss);
}

HeapStackBegin = BSSRAM + BSSSize;
HeapStackEnd = ORIGIN(RAM) + LENGTH(RAM);
HeapStackSize = HeapStackEnd - HeapStackBegin;
MaxIRQ = 5;

ENTRY(runtime_Start)
